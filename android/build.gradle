apply plugin: 'com.android.library'

// Android dependencies
def DEFAULT_COMPILE_SDK_VERSION                     = 28
def DEFAULT_TARGET_SDK_VERSION                      = 28
def DEFAULT_APP_COMPAT_VERSION                      = "1.3.1"

// Plugin dependencies
def DEFAULT_GOOGLE_PLAY_SERVICES_LOCATION_VERSION   = "18.0.0"
def DEFAULT_OK_HTTP_VERSION                         = "3.12.13"
def DEFAULT_ANDROID_PERMISSIONS_VERSION             = "0.1.8"
def DEFAULT_EVENTBUS_VERSION                        = "3.2.0"
def DEFAULT_LOCAL_BROADCAST_MANAGER_VERSION         = "1.0.0"

def useAndroidX = (project.hasProperty("android.useAndroidX")) ? project.property("android.useAndroidX") : false

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
}

repositories{
    maven {
        url './libs'
    }
}

dependencies {
    // Prefer appCompatVersion vs supportLibVersion.
    def appCompatVersion = safeExtGet('appCompatVersion', DEFAULT_APP_COMPAT_VERSION)
    def playServicesLocationVersion = safeExtGet('googlePlayServicesLocationVersion', DEFAULT_GOOGLE_PLAY_SERVICES_LOCATION_VERSION)
    def okHttpVersion = safeExtGet('okHttpVersion', DEFAULT_OK_HTTP_VERSION)
    def androidPermissionsVersion = safeExtGet('androidPermissionsVersion', DEFAULT_ANDROID_PERMISSIONS_VERSION)
    def eventBusVersion = safeExtGet('eventBusVersion', DEFAULT_EVENTBUS_VERSION)
    def localBroadcastManagerVersion = safeExtGet('localBroadcastManagerVersion', DEFAULT_LOCAL_BROADCAST_MANAGER_VERSION)
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    // tslocationmanager.aar
    if (useAndroidX) {
        api(group: 'com.transistorsoft', name:'tslocationmanager', version: '+')
        implementation "androidx.localbroadcastmanager:localbroadcastmanager:$localBroadcastManagerVersion"
    } else {
        api(group: 'com.transistorsoft', name:'tslocationmanager-reverse', version: '+')
    }

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesLocationVersion"
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation "io.github.nishkarsh:android-permissions:$androidPermissionsVersion"
}
